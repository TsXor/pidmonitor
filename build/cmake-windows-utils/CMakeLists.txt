cmake_minimum_required(VERSION 3.15)

set(AVAILABLE_ARCH "x32" "x64")
# default to x64
if (NOT DEFINED ARCH)
    message(STATUS "default ARCH to x64")
    set(ARCH "x64")
endif()
string(SUBSTRING ${ARCH} 1 -1 ARCH_N)
# architecture check
list(FIND AVAILABLE_ARCH ${ARCH} avail_state)
if (${avail_state} EQUAL -1)
    message(FATAL_ERROR "unsupported architecture")
endif()
if(${ARCH} STREQUAL "x64" AND ${CMAKE_HOST_SYSTEM_PROCESSOR} STREQUAL "x86")
    message(FATAL_ERROR "cannot make 64-bit program on 32-bit toolchain")
endif()

function(import_extern_dll target_name dll_name path)
    if(MSVC)
        set(lib_type SHARED)
    else()
        set(lib_type MODULE)
    endif()
    if(${path} STREQUAL MS_BUILTIN)
        if(MSVC)
            set(dll_path ${dll_name}.dll)
            set(lib_path ${dll_name}.lib)
        else()
            if(${ARCH} STREQUAL "x32" AND ${CMAKE_HOST_SYSTEM_PROCESSOR} STREQUAL "AMD64")
                set(dll_path C:/Windows/SysWoW64/${dll_name}.dll)
                set(lib_path C:/Windows/SysWoW64/${dll_name}.lib)
            else()
                set(dll_path C:/Windows/System32/${dll_name}.dll)
                set(lib_path C:/Windows/System32/${dll_name}.lib)
            endif()
        endif()
    else()
        set(dll_path ${path}/${dll_name}.dll)
        set(lib_path ${path}/${dll_name}.lib)
    endif()
    add_library(${target_name} ${lib_type} IMPORTED)
    set_property(TARGET ${target_name} PROPERTY
                IMPORTED_LOCATION ${dll_path})
    set_property(TARGET ${target_name} PROPERTY
                IMPORTED_IMPLIB ${lib_path})
endfunction()

set(UAC_RC_PATH ${CMAKE_CURRENT_LIST_DIR}/uac.rc)

function(target_uac_admin target_name)
    if(MSVC)
        set_target_properties(${target_name} PROPERTIES
                              LINK_FLAGS " /MANIFESTUAC:\"level='requireAdministrator' uiAccess='false'\" ")
    else(MINGW)
        target_sources(${target_name} PRIVATE ${UAC_RC_PATH})
    endif()
endfunction()

function(target_use_console_subsystem target_name)
    if(MSVC)
        target_link_options(${target_name} PRIVATE /SUBSYSTEM:CONSOLE)
    else(MINGW)
        target_link_options(${target_name} PRIVATE -mconsole)
    endif()
endfunction()
